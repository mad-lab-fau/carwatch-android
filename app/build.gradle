apply plugin: 'com.android.application'

android {
    compileSdk 34
    namespace 'de.fau.cs.mad.carwatch'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    def getGitHash = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--short', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }

    def getGitDate = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'log', '-1', '--date=format:%Y%m%d_%H%M', '--pretty=format:%cd'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }

    defaultConfig {
        applicationId "de.fau.cs.mad.carwatch"
        minSdkVersion 21
        targetSdkVersion 34

        def versionMajor = 2
        def versionMinor = 5
        def versionPatch = 0
        versionName "${versionMajor}.${versionMinor}.${versionPatch}" + "_" + getGitDate() + "-" + getGitHash()
        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
    }

    buildFeatures {
        dataBinding true
        buildConfig true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    namespace 'de.fau.cs.mad.carwatch'

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.navigation:navigation-fragment:2.8.9'
    implementation 'androidx.navigation:navigation-ui:2.8.9'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'joda-time:joda-time:2.10.3'
    implementation 'com.orhanobut:logger:2.2.0'
    implementation group: 'org.zeroturnaround', name: 'zt-zip', version: '1.8'

    implementation 'com.google.android.gms:play-services-mlkit-barcode-scanning:18.3.1'

    def room_version = "2.6.1"
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version" // or kapt for Kotlin

    // Lifecycle (ViewModel, LiveData, etc.)
    def lifecycle_version = "2.8.7"
    implementation "androidx.lifecycle:lifecycle-viewmodel:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    annotationProcessor "androidx.lifecycle:lifecycle-common:$lifecycle_version" // or kapt for Kotlin

}


apply plugin: 'com.google.gms.google-services'
